Name      SPACER;           /** Serial to PArallel ControllER **/
Partno    ATF1508AS;
Date      28/August/2021;
Revision  Release 1.21;
Designer  Bernardo Kastrup;
Company   The Byte Attic;
Assembly  None;
Location  The Netherlands;
Device    F1508ISPPLCC84;   /** ATF1508AS-10JU84, with In-System Programmability **/


/** Provided AS-IS, without guarantees of any kind                        **/
/** This is NOT a commercial product and has not been exhaustively tested **/


/*******************************************************************************/
/** TURN OFF CUPL COMPILER MINIMIZATION AND OPTIMIZATION TO GET THIS TO WORK! **/
/*******************************************************************************/


/** Just some general settings, check the CUPL manual for details **/
PROPERTY ATMEL {POWER_RESET = OFF};
PROPERTY ATMEL {PIN_KEEP = ON};
PROPERTY ATMEL {PREASSIGN = KEEP};


/**************/
/** I/O pins **/
/**************/
Pin [81..79,77..73,70..67,65..63,61,60,58..54,52,51] = [SLRO0..23]; /** Shift Left Register Outputs      **/
/** SLRO23..16 are the data bits, SLRO15..0 are the address bits, as per this mapping:                   **/
/** 23, 22, 21, 20, 19, 18, 17, 16,  15,  14,  13,  12,  11,  10,  9,  8,  7,  6,  5,  4,  3,  2,  1,  0 **/
/** D7, D6, D5, D4, D3, D2, D1, D0, A15, A14, A13, A12, A11, A10, A9, A8, A7, A6, A5, A4, A3, A2, A1, A0 **/

/****************/
/** INPUT pins **/
/****************/
pin 1  = RST;               /** Global reset for SPACER **/
/** From the oscillator **/
pin 83 = CLK;               /** Clock input from the buffer **/
/** From the dual-ported memories **/
pin 84 = VBUSY;             /** From dual-ported video SRAM, active low **/
pin 2  = CBUSY;             /** From dual-ported character SRAM, active low **/
/** From CAT **/
pin 9  = SI;                /** Serial Input from CAT, connected to pin SO in CAT **/
Pin 10 = SC;                /** Shift Clock, input from CAT **/
Pin 11 = AOE;               /** Address Output Enable for Shift Left Register, from CAT **/
pin 12 = RWCAT;             /** R/!W signal from CAT **/
pin 15 = LD;                /** Latch Data signal from CAT to acquire a byte from data bus **/
Pin 16 = CPUSLC;            /** CPU SeLeCt, input from CAT: 1 = Z80, 0 = 6502 **/
Pin 17 = CPUIRQ;            /** CPU Interrupt ReQuest, from CAT **/
pin 18 = CPUGO;             /** To !pause/continue CPU processing, from CAT **/
pin 20 = CPURST;            /** To reset the CPU, from CAT **/
pin 21 = CPUSPD;            /** Defines whether 4 or 8 MHz CPU clock, from CAT **/
/** From the CPUs **/
pin 28 = RWB6502;           /** R/!WB signal from the 6502 **/
pin 34 = RDZ80;             /** !RD signal from the Z80 **/
pin 35 = WRZ80;             /** !WR signal from the Z80 **/
pin 36 = MREQZ80;           /** Memory REQuest from Z80, indicates address on address bus is stable **/

/*****************/
/** OUTPUT pins **/
/*****************/
pin 25 = BE;                /** Buffer Enable, to hold the input clock (CLK) during power up **/
/** For CAT **/
pin 22 = SO;                /** Serial Output for CAT, connected to pin SI in CAT **/
pin 24 = CATCLK;            /** Clock output for CAT **/
/** RW signals for the memories **/
pin 4  = RWVM;              /** Read/!Write signal for VMEM **/
pin 6  = RWCM;              /** Read/!Write signal for CMEM **/
pin 45 = OE;                /** !Output Enable for all memories **/
pin 46 = WELM;              /** !Write Enable signal for LMEM **/
pin 49 = WEHM;              /** !Write Enable signal for HMEM **/
/** Chip Enable signals for the memories **/
pin 5  = CEVM;              /** !Chip Enable signal for Character Memory **/
pin 8  = CECM;              /** !Chip Enable signal for Video Memory **/
pin 48 = CELM;              /** !Chip Enable signal for Low Memory **/
pin 50 = CEHM;              /** !Chip Enable signal for High Memory **/
/** For the CPUs **/
pin 27 = CLK6502;           /** CLocK for the 6502 **/
pin 29 = BE6502;            /** Bus Enable signal for the 6502 **/
pin 30 = RDY6502;           /** ReaDY signal for 6502 **/
pin 31 = NMIB6502;          /** !NMI signal for the 6502 **/
pin 33 = RST6502;           /** !Reset the 6502 **/
pin 37 = BUSREQZ80;         /** !BUS REQuest signal for the Z80 **/
pin 39 = CLKZ80;            /** CLocK for the Z80 **/
pin 40 = NMIZ80;            /** !Non-Maskable Interrupt request for Z80 **/
pin 41 = RSTZ80;            /** !Reset the Z80 **/
pin 44 = WAITZ80;           /** !WAIT signal for the Z80 **/

/** Buried states **/
Pinnode = DELAY;      /** Internal delay state useful for controling the 6502 **/
Pinnode = [C1..0];    /** Internal states of the clock divider counter **/
Pinnode = [SLR0..23]; /** Shift Left Register internal states, defined separately to SLRO0..23 **/
                      /** so as to circumvent a bug in the CUPL software. It ensures the shif register **/
                      /** uses the output of the flip-flops, instead of the .io signal after the buffers. **/
Pinnode = [SRR0..7];  /** Shift Right Register internal states **/
Field ShiftLeftRegister = [SLR0..23];  /** Handy way to refer to Shift Left Register as a whole **/
Field ShiftOneLeft = [SLR1..23, SI];   /** Shifting left to fill SLR with serial data from CAT **/
Field DATABUS = [SLRO16..23].io;       /** Data Bus bits connected to the pins of Shift Left Register **/
Field ShiftRightRegister = [SRR0..7];  /** Handy way to refer to Shift Right Register as a whole **/
Field ShiftOneRight = ['b'0, SRR0..6]; /** Shifting right to provide SRR parallel data to CAT **/


/***********************************************/
/** Enable the buffer (74HC244) upon power up **/
/***********************************************/
BE = 'b'1;


/*****************************/
/** Memory management logic **/
/*****************************/

/** First, the Chip Enable (CE) signals for the four memories, which are address-dependent **/
/** The internal variable ENABLE below is meant to force the CE signals to wait until the address bus is stable **/
/** The first product term is for CAT, the second for the 6502, and the third for the Z80 **/
ENABLE = AOE # (!CPUSLC & CPUGO) # !MREQZ80;

/** BELOW: The .io suffix corresponds to the content of the bus line attached to the respective pin **/
/** REMEMBER: chip enable signals are active low **/
CEVM = !( SLRO15.io &   SLRO14.io & SLRO13.io & SLRO12.io  &  SLRO11.io & ENABLE); /** i.e. A15 & A14 & A13 etc. **/
CECM = !( SLRO15.io &   SLRO14.io & SLRO13.io & SLRO12.io  & !SLRO11.io & ENABLE);
CEHM = !( SLRO15.io & !(SLRO14.io & SLRO13.io & SLRO12.io) &              ENABLE);
CELM = !(!SLRO15.io &                                                     ENABLE);

/** Now, the Read (including Output Enable) and Write (including Write Enable) signals for the four memories **/
/** The equations are self-explanatory. CLK6502 is ANDed with RWB6502 to shorten the write pulse and prevent miswrites. **/
RWVM = !((!RWCAT # (!RWB6502 & CLK6502) # !WRZ80) & !CEVM);
RWCM = !((!RWCAT # (!RWB6502 & CLK6502) # !WRZ80) & !CECM);
WEHM = !((!RWCAT # (!RWB6502 & CLK6502) # !WRZ80) & !CEHM);
WELM = !((!RWCAT # (!RWB6502 & CLK6502) # !WRZ80) & !CELM);

/** Now we need to use AOE, CPUGO and CPUSLC in the equation for OE because RWCAT and RWB6502 are always pulled high **/
OE   = !((RWCAT & AOE) # (RWB6502 & CLK6502 & CPUGO & !CPUSLC) # !RDZ80);

/** Finally, some internal signals to manage DP memory contention **/
PAUSECAT  = !RWCAT & (!VBUSY # !CBUSY);             /** Pause CAT if it's writing and a DP memory flags BUSY **/
PAUSE6502 = !RWB6502 & CLK6502 & (!VBUSY # !CBUSY); /** Promptly pause 6502 if it's trying to write and DP mem is BUSY **/
PAUSEZ80  = (!MREQZ80 & RDZ80) & (!VBUSY # !CBUSY); /** Pause Z80 if it's accessing memory and NOT reading, **/
                                                    /** and then a DP memory flags BUSY **/
                                                    /** (!WRZ80 goes active too late, so need to use !MREQZ80 **/
                                                    /**  ANDed with RDZ80 instead) **/


/***********************/
/** CPU control logic **/
/***********************/
DELAY.d    =  BE6502;         /** DELAY latches the current state of BE6502... **/
DELAY.ck   = !CLK6502;        /** On the next falling edge of the 6502's clock, creating a one-cycle delay line **/
DELAY.ar   =  RDY6502 # RST;  /** The delay line is reset when the 6502 is released (i.e. RDY6502 = 1), so to... **/
                              /** maintain synch with RDY6502 **/

BE6502.d   = !((CPUSLC # !CPUGO) & !RDY6502);  /** Disable 6502's buses if 6502 is not selected or not GO, provided... **/
                                               /** ...that the 6502 is already halted. Otherwise enable them. **/
BE6502.ck  = !CLK6502;                         /** But do so only on the next falling edge of the 6502's clock **/
BE6502.ap  = !CPUSLC & CPUGO;                  /** If 6502 is selected and GO, enable its buses promptly, asynchronously **/
BE6502.ar  =  RST;                             /** This is just the default reset condition **/

RDY6502.d  = !CPUSLC & CPUGO & !PAUSE6502 & DELAY;/** Release 6502 if it is selected, GO, not pausing, and its buses **/
                                                  /** have already been enabled for at least one clock cycle (DELAY) **/
                                                  /** Otherwise halt the 6502, or keep it halted, as the case may be **/
RDY6502.ck =  CLK6502;                            /** But do so only on the next rising edge of the 6502's own clock **/
RDY6502.ar =  PAUSE6502 # RST;                    /** Promptly halt the 6502 aynchronously if PAUSE6502 is asserted  **/

/** The lines above mean the following:                                                                                **/
/** The 6502 can be asynchronously halted (RDY6502 = 0) as soon as PAUSE6502 is asserted by one of the DP memories.    **/
/** Once halted, the 6502 can only be released again (RDY6502 = 1) synchronously, on the next RISING edge of its clock.**/
/** The 6502's buses can be asynchronously enabled (BE6502 = 1) as soon as CAT asserts CPUGO (with !CPUSLC).           **/
/** Once enabled, the 6502's buses can only be disabled (BE6502 = 0) synchronously, on the next FALLING edge of clock. **/
/** CAT can only halt the 6502 (by making e.g. CPUGO = 0) synchronously, on a RISING edge of the 6502's clock.         **/
/** CAT can only disable the 6502's buses (by making e.g. CPUGO = 0) synchronously, on the next FALLING edge of clock. **/
/** This way, there is always a half clock cycle between CAT's halting the 6502 and disabling its buses, which gives   **/
/** the 6502 enough time to complete any ongoing memory access. Notice that the 6502's buses can NEVER be disabled     **/
/** while the 6502 is running; only if the 6502 is already halted (RDY6502 = 0) can SPACER make BE6502 = 0.            **/
/** Similarly, when CAT releases the 6502 (by making CPUGO = 1) after a halt period, the circuit above ensures that    **/
/** the 6502's buses have already been re-enabled for at least one clock cycle before the 6502 is actually released    **/
/** (i.e. by making RDY6502 = 1). This is done by delaying recognition that the buses are enabled (with the DELAY      **/
/** state). The delay state is reset once the 6502 has been released (i.e. RDY6502 = 1), so to maintain synch with     **/
/** RDY6502. The idea is to prevent the 6502 from trying to access memory before its buses are ready for it.           **/

NMIB6502   = !(!CPUSLC  &  CPUIRQ);            /** !Interrupt 6502 if it is selected and CAT wants to interrupt it **/
RST6502    = !(!CPUSLC  &  CPURST);            /** !Reset the 6502 if it is selected and CAT wants to reset it **/
BUSREQZ80  = !(!CPUSLC  # !CPUGO);             /** !Request buses from Z80 if the latter is not selected or is not GO **/
WAITZ80    = !( CPUSLC  &  CPUGO & PAUSEZ80);  /** !Wait if Z80 is selected and GO, but there is a PAUSE active **/
NMIZ80     = !( CPUSLC  &  CPUIRQ);            /** !Interrupt the Z80 if it's selected and CAT wants to interrupt it*/
RSTZ80     = !( CPUSLC  &  CPURST);            /** !Reset the Z80 if it's selected and CAT wants to reset it **/


/****************************/
/** Clock management logic **/
/****************************/

/** BELOW: Clock divider based on a counter (using T flip-flops to count) **/
C1.t = C0;
C0.t = 'b'1;
[C1..0].ck = CLK;                           /** CLK is the input from oscillator, running at 16MHz **/
[C1..0].ar = RST;                           /** Global asynchronous reset also resets this counter **/

/** Now to the three separate clocks **/
RAWCPUCLK = (!CPUSPD & C1) # (CPUSPD & C0); /** An internal variable that selects the CPU clock speed, used below **/
CLK6502   =  !CPUSLC & RAWCPUCLK;           /** Provide clock to 6502 only if the latter is selected **/
CLKZ80    =   CPUSLC & RAWCPUCLK;           /** Provide clock to Z80 only if the latter is selected **/
CATCLK    =  !PAUSECAT & CLK;               /** Withdraw clock from CAT if the latter is writing to a BUSY DP memory **/


/*********************/
/** Shift Registers **/
/*********************/
[SLRO15..0].oe  =  AOE;                     /** These are the address bits **/
[SLRO23..16].oe = !RWCAT;                   /** These are the data bits **/
[SLRO0..23] = ShiftLeftRegister;            /** The compiler should get rid of this redundancy **/
ShiftLeftRegister.ck =	SC;
ShiftLeftRegister.ar =	RST;
ShiftLeftRegister.d  =	ShiftOneLeft;

ShiftRightRegister.ck = SC;
ShiftRightRegister.ar = RST;
ShiftRightRegister.d  = ( LD & DATABUS) #      /** Latch data bus value on next clock tic **/
                        (!LD & ShiftOneRight); /** Shift contents to the right on next clock tic **/
/**
The statement above expands to the following statements:
(which make the structure of the shift register explicit)
SRR7.d = (LD & SLRO23.io) # (!LD & SRR6);
SRR6.d = (LD & SLRO22.io) # (!LD & SRR5);
SRR5.d = (LD & SLRO21.io) # (!LD & SRR4);
SRR4.d = (LD & SLRO20.io) # (!LD & SRR3);
SRR3.d = (LD & SLRO19.io) # (!LD & SRR2);
SRR2.d = (LD & SLRO18.io) # (!LD & SRR1);
SRR1.d = (LD & SLRO17.io) # (!LD & SRR0);
SRR0.d = (LD & SLRO16.io) # (!LD & 'b'0);
**/
SO = SRR7;
